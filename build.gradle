buildscript {
    repositories {
        mavenLocal()
        ["public", "thirdparty", "releases"].each { r ->
            maven {
                credentials {
                    username nexusUserName
                    password nexusPassword
                }
                url "${nexusBaseUrl}/repositories/${r}"
            }
        }
    }

    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:1.1.1"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.moowork.node'

node {
    version = '8.12.0'
    yarnVersion = '1.9.2'
    npmVersion = '3.10.9'
    download = true
    nodeModulesDir = file("${rootProject.projectDir}")
}

group 'com.xebialabs.xlrelease.plugins'

sourceSets {
    // js are processed by webpack
    main {
        resources {
            exclude '**/*.js'
        }
    }
}

task yarnRunBuild(type: YarnTask) {
    args = ['run', 'build']
}

yarnRunBuild.dependsOn yarn_install
processResources.dependsOn yarnRunBuild